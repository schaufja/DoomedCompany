Class ConsoleLetter : Actor
{
    string character;

    override void BeginPlay(void)
    {
        Super.BeginPlay();
        character = "A";
    }

    override void Tick(void)
    {
        Super.Tick();
        UpdateCharacter();
    }

    Default
    {
        +NOGRAVITY
        +BRIGHT
    }

    States
    {
        Spawn:
            CONS A 0;
        Stay:
            #### # 1;
            Loop;
        Letters:
            CONS ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
        Numbers:
            CONN ABCDEFGHIJ 1;
        Symbols:
            CSYM ABCDEFG 1;
            stop;
    }

    void UpdateCharacter(void)
    {
        string letterString = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
        string numberString = "0123456789";
        string symbolString = "$>:[]/*";
        int letterIndex = letterString.IndexOf(character);
        int numberIndex = numberString.IndexOf(character);
        int symbolIndex = symbolString.IndexOf(character);

        if(letterIndex != -1)
        {
            sprite = GetSpriteIndex("CONS");
            frame = letterIndex;
        }
        else if (numberIndex != -1)
        {
            sprite = GetSpriteIndex("CONN");
            frame = numberIndex;
        }
        else if (symbolIndex != -1)
        {
            sprite = GetSpriteIndex("CSYM");
            frame = symbolIndex;
        }
        else
        {
            sprite = GetSpriteIndex("TNT1");
            frame = 0;
        }
    }

    void SetCharacter(string inCharacter)
    {
        if (inCharacter.Length() != 1)
        {
            return;
        }

        character = inCharacter;
    }
}