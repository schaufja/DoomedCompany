Class ConsoleLetter : Actor
{
    string character;
    string colorTint;

    override void BeginPlay(void)
    {
        Super.BeginPlay();
        character = "A";
    }

    override void Tick(void)
    {
        Super.Tick();
        UpdateCharacter();
    }

    Default
    {
        height 0;
        radius 0;
        Translation "None";

        +WALLSPRITE
        +NOGRAVITY
        +BRIGHT
        -SOLID
    }

    States
    {
        Spawn:
            CONS A 0;
        Stay:
            #### # 1;
            Loop;
        Letters:
            CONS ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
            stop;
        LowerLetters:
            CONL ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
            stop;
        Numbers:
            CONN ABCDEFGHIJ 1;
            stop;
        Symbols:
            CSYM ABCDEFG 1;
            stop;
    }

    void UpdateCharacter(void)
    {
        string letterString = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
        string lowerLetterString = "abcdefghijklmnopqrstuvwxyz";
        string numberString = "0123456789";
        string symbolString = "$>:[]/*"; //Very angry symbol person with money hair
        int letterIndex = letterString.IndexOf(character);
        int lowerLetterIndex = lowerLetterString.IndexOf(character);
        int numberIndex = numberString.IndexOf(character);
        int symbolIndex = symbolString.IndexOf(character);

        if(letterIndex != -1)
        {
            sprite = GetSpriteIndex("CONS");
            frame = letterIndex;
        }
        else if (lowerLetterIndex != -1)
        {
            sprite = GetSpriteIndex("CONL");
            frame = lowerLetterIndex;
        }
        else if (numberIndex != -1)
        {
            sprite = GetSpriteIndex("CONN");
            frame = numberIndex;
        }
        else if (symbolIndex != -1)
        {
            sprite = GetSpriteIndex("CSYM");
            frame = symbolIndex;
        }
        else
        {
            sprite = GetSpriteIndex("TNT1");
            frame = 0;
        }
    }

    void SetCharacter(string inCharacter, string inColorTint = "None")
    {
        if (inCharacter.Length() != 1)
        {
            return;
        }

        character = inCharacter;
        A_SetTranslation(colorTint);
    }
}