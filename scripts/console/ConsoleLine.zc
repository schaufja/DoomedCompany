Class ConsoleLine : Actor
{
    int characterLength;
    double baseOffset;
    double screenScale;
    array <ConsoleLetter> lineString;

    override void BeginPlay(void)
    {
        Super.BeginPlay();
    }

    Default
    {
        height 0;
        radius 0;
        +NOGRAVITY
        -SOLID
    }

    void SetupString(void)
    {
        //screenScale = 0.125;

        double currentOffset = 0;

        Vector3 forwards = (cos(angle), sin(angle), 0);

        for(int i = 0; i < characterLength; i++)
        {
            ConsoleLetter character = ConsoleLetter(Actor.Spawn("ConsoleLetter", pos + currentOffset * forwards, ALLOW_REPLACE));
            //console.printf("Created character at (%f,%f,%f)",character.pos.x,character.pos.y,character.pos.z);
            character.scale = (screenScale, screenScale);
            character.angle = angle - 90;
            lineString.Push(character);
            currentOffset += baseOffset;
        }

        ClearString();
    }

    void PostString(string input)
    {
        input.Truncate(characterLength);
        for(int i = 0; i < input.Length(); i++)
        {
            lineString[i].SetCharacter(input.Mid(i, 1));
        }
    }

    void ClearString(void)
    {
        for(int i = 0; i < lineString.Size(); i++)
        {
            lineString[i].SetCharacter(" ");
        }
    }
}