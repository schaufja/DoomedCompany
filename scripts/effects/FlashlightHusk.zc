Class FlashlightHusk : Actor
{
    int cycles;

    override void BeginPlay(void)
    {
        cycles = 3 * 35; //3 seconds
    }

    Default
    {
        +NOBLOCKMAP
        Scale 0.5;
    }
    States
    {
    Spawn:
        FLAS C 1 NoDelay
        {
            if(cycles > 0)
            {
                A_SpawnItemEx("MagicSmoke", xofs: 0, yofs: 0, zofs: random(3,8), xvel: (0.1)*Random(1, 10), yvel: 0, zvel: (0.1)*Random(15, 20), angle: Random(0, 360), flags: 128);
            }
            cycles--;

            if(cycles < (-10 * 35))
            {
                A_FadeOut(0.1);
            }
        }
        Loop;
    }
}

Class FlashlightShot : FatShot
{
    States
	{
	Spawn:
		MANF AB 4 BRIGHT;
		Loop;
	Death:
		MISL B 8 BRIGHT A_QuakeEx( 16, 16, 16, 60, 0, 300, sfx: "" );
		MISL C 6 BRIGHT;
		MISL D 4 BRIGHT;
		Stop;
	}
}

Class MagicSmoke : Actor
{
    double alphaMod;
    double scaleMod;

    override void BeginPlay(void)
    {
        alphaMod = 0.6;
        scaleMod = frandom(0.2, 0.6);
        scale = (scaleMod, scaleMod);
    }
    Default
    {
        Height 1;
        Radius 1;
        RenderStyle "add";
        Scale 0.4;
        Alpha 0.65;
        Translation "MagicSmokeTint";
        +Missile
        +NoGravity
        +NoBlockMap
    }
    States
    {
    Spawn:
        STEM ABCDEFGHIJKL 2
        {
            A_SetTranslucent(alphaMod, 1);
            alphaMod -= 0.05;
        }
        Stop;
    }
}